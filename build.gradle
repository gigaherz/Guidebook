buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

repositories {
    maven {
        url 'http://dogforce-games.com/maven'
    }

    maven {
        url 'https://maven.mcmoddev.com'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = "2.5.0"
group = "gigaherz.guidebook" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Guidebook-1.12.2"

minecraft {
    version = "1.12.2-14.23.4.2707"
    runDir = "run"

    mappings = "snapshot_20180601"

    replace "@VERSION@", project.version
    replaceIn "GuidebookMod.java"
}

configurations {
    // configuration that holds jars to embed inside the jar
    embed
}

dependencies {
    deobfCompile "gigaherz.commons:gigaherz.commons-1.12.1:0.6.4"
    embed "gigaherz.commons:gigaherz.commons-1.12.1:0.6.4"

    deobfCompile "net.darkhax.gamestages:GameStages-1.12.2:1.0.80"
    deobfCompile "net.darkhax.bookshelf:Bookshelf-1.12.2:2.3.526"

    //compile fileTree(dir: 'libs', include: '*.jar')
}

jar {
    into('/') {
        from configurations.embed
    }

    manifest {
        attributes("ContainedDeps": configurations.embed.collect { it.getName() }.join(' '))
    }
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

tasks.publish.dependsOn build
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            from components.java

            artifact sourceJar
        }
    }

    repositories {
        maven {
            url "$projectDir/../CommonMaven"
        }
    }
}

