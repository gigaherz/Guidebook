buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'

repositories {
    maven {
        url "https://maven.tterrag.com/"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven { url 'https://jitpack.io' }
    mavenLocal()
}

version = "3.0.5"
group = "gigaherz.guidebook" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Guidebook-1.15.2"

sourceCompatibility = 14
targetCompatibility = 8

compileJava {
    sourceCompatibility = 14
    targetCompatibility = 8

    options.compilerArgs = [
            "--release", "8" // Avoid using Java 12 APIs
    ]
    options.fork = true
}

minecraft {
    mappings channel: 'snapshot', version: '20200517-1.15.1'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            property 'forge.logging.noansi', "false"
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            property 'forge.logging.noansi', "false"

            singleInstance true
        }
    }
}

dependencies {
    minecraft('net.minecraftforge:forge:1.15.2-31.2.0') {
        transitive=false
    }

    //compileOnly fg.deobf("mezz.jei:jei-1.13.2:5.0.0.25:api")
    //runtimeOnly fg.deobf("mezz.jei:jei-1.13.2:5.0.0.25")

    annotationProcessor 'com.github.bsideup:jabel:ed36f18'
}

jar {
    manifest {
        attributes([
                "Specification-Title": "guidebook",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Maven-Artifact":"${project.group}:${project.archivesBaseName}:${project.version}"
        ])
    }
}

tasks.publish.dependsOn build
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            from components.java

            //artifact sourceJar

            pom.withXml {
                asNode().dependencies.dependency.each { dep ->
                    if(dep.groupId.last().value().last() in ["net.minecraftforge"]) {
                        assert dep.parent().remove(dep)
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url "$projectDir/../CommonMaven"
        }
    }
}
